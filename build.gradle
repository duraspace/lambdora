buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "net.ltgt.gradle:gradle-errorprone-plugin:0.0.13"
    }
}

allprojects {
    group 'org.fcrepo.lambdora'
    version '0.1.0-SNAPSHOT'
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'net.ltgt.errorprone'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    checkstyle {
        configFile = rootProject.file('config/checkstyle/checkstyle.xml')
        configProperties = [
                'configDir': rootProject.file('config/checkstyle'),
                'baseDir'  : rootDir,
        ]
        toolVersion = '7.3'
    }

    dependencies {
        testCompile([group: 'junit', name: 'junit', version: '4.12'],
                    [group: 'com.amazonaws', name: 'DynamoDBLocal', version: '1.11.0'],
                    [group: 'org.mockito', name: 'mockito-core', version: '2.+']
        )
    }

    repositories {
        mavenCentral()
        maven {
            url 'http://dynamodb-local.s3-website-us-west-2.amazonaws.com/release'
        }
        repositories { jcenter() }
    }

    task copyNativeDeps(type: Copy) {
        from (configurations.testCompile) {
            include "*.dylib"
            include "*.so"
            include "*.dll"
        }
        into 'build/libs'
    }

    test.dependsOn copyNativeDeps
    test.doFirst {
        systemProperty "java.library.path", 'build/libs'
    }

}


project(':lambdora-http-api') {
    dependencies {
        compile project(':lambdora-db')
        compile project(':lambdora-service')
        compile(
                'com.amazonaws:aws-lambda-java-core:1.1.0',
                'com.amazonaws:aws-lambda-java-log4j:1.0.0',
                'com.fasterxml.jackson.core:jackson-core:2.9.2',
                'com.fasterxml.jackson.core:jackson-databind:2.9.2',
                'com.fasterxml.jackson.core:jackson-annotations:2.9.2',
                'org.apache.httpcomponents:httpcore:4.4.8',
                'org.apache.httpcomponents:httpmime:4.5.3',
                'javax.ws.rs:javax.ws.rs-api:2.1'
        )
    }

    // http://docs.aws.amazon.com/lambda/latest/dg/create-deployment-pkg-zip-java.html
    task buildZip(type: Zip) {
        baseName = "${project.name}"
        from compileJava
        from processResources
        into('lib') {
            from configurations.runtime
        }
    }

    build.dependsOn buildZip
}

project(':lambdora-db') {
    dependencies {
        compile project(':lambdora-common')
    }
}

project(':lambdora-service') {
    dependencies {
        compile project(':lambdora-db')
        compile (
            'org.apache.jena:jena-core:3.4.0',
            'org.apache.jena:jena-iri:3.4.0',
            'org.apache.jena:jena-arq:3.4.0'
        )
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.2.1'
}
