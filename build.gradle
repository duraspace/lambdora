version '0.1.0-SNAPSHOT'
group 'org.fcrepo.lambdora'

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "net.ltgt.gradle:gradle-errorprone-plugin:0.0.13"
    }
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'net.ltgt.errorprone'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    checkstyle {
        configFile = rootProject.file('config/checkstyle/checkstyle.xml')
        configProperties = [
                'configDir': rootProject.file('config/checkstyle'),
                'baseDir'  : rootDir,
        ]
        toolVersion = '7.3'
    }

    dependencies {
        testCompile([group: 'junit', name: 'junit', version: '4.12'],
                    [group: 'com.amazonaws', name: 'DynamoDBLocal', version: '1.11.0'])
    }

    repositories {
        mavenCentral()
        maven {
            url 'http://dynamodb-local.s3-website-us-west-2.amazonaws.com/release'
        }

    }

    task copyNativeDeps(type: Copy) {
        from (configurations.testCompile) {
            include "*.dylib"
            include "*.so"
            include "*.dll"
        }
        into 'build/libs'
    }

    test.dependsOn copyNativeDeps
    test.doFirst {
        systemProperty "java.library.path", 'build/libs'
    }

}


project(':lambdora-http-api') {
    dependencies {
        compile project(':lambdora-db')
    }
}

project(':lambdora-db') {
    dependencies {
        compile project(':lambdora-common')
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.2.1'
}
